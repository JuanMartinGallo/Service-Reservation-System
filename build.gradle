plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    alias(libs.plugins.springBoot)
    alias(libs.plugins.dependencyManagement)
    alias(libs.plugins.dependencyCheck)
    alias(libs.plugins.benManesVersions)
    alias(libs.plugins.versionCatalogUpdate)
    alias(libs.plugins.sonarqube)
}

group = 'com.srs'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation libs.springBootStarterWebflux
    implementation libs.springBootStarterValidation

    // JSON
    implementation libs.jacksonDatabind
    implementation libs.jacksonDatatypeJsr310

    // Data
    implementation libs.flywayCore
    implementation libs.flywayPostgresql
    implementation libs.springBootStarterDataR2dbc
    implementation libs.springJdbc
    runtimeOnly libs.postgresql
    runtimeOnly libs.r2dbcPostgresql

    // Lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // Documentation
    implementation libs.springdocOpenapiStarterWebfluxUi

//    // Metrics & Monitoring
//    implementation libs.springBootStarterActuator
//    implementation libs.micrometerRegistryPrometheus

    // Resilience
    implementation libs.springRetry
    implementation libs.springAspects
    implementation libs.aspectjrt
    implementation libs.aspectjweaver

    // Unit Testing
    testImplementation libs.springBootStarterTest
    testImplementation libs.reactorTest
    testImplementation libs.testng

    // Integration Test
    testImplementation libs.springBootTestcontainers
    testImplementation libs.testcontainersJunitJupiter
    testImplementation libs.testcontainersR2dbc
    testImplementation libs.testcontainersPostgresql

    // Security
    implementation libs.springSecurity
    testImplementation libs.springSecurityTest
    implementation libs.springSecurityOauth2ResourceServer

    // JWT
    implementation libs.jsonWebToken
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

tasks.register('checkstyle') {
    reports {
        xml.required = false
        html.required = true
    }
}

jacoco {
    toolVersion = "0.8.11"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.required = true
        csv.required = false
        html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
    }
}

bootRun {
    jvmArgs = ['-Dspring.profiles.active=local']
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-parameters']
}

// Dependency Maintenance
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
